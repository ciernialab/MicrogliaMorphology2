// Color-by-cluster morphology script
// Jenn Kim
// July 25, 2024
 
// FUNCTIONS

// find ROIs by name
// function adapted from:
// https://forum.image.sc/t/selecting-roi-based-on-name/3809 
function findRoiWithName(roiName) { 
	nR = roiManager("Count"); 
 
	for (i=0; i<nR; i++) { 
		roiManager("Select", i); 
		rName = Roi.getName(); 
		if (matches(substring(rName, 0, 5), substring(roiName, 0, 5))) { 
			return i; 
		} 
	} 
} 

// Function to open the file
function openFile(filePath) {
    if (File.exists(filePath)) {
        open(filePath);
    } 
}

// Function to list files and search for matching filenames
function listFiles(dir, fileList, searchString) {
    found_filename = "not_found";
    for (i = 0; i < fileList.length; i++) {
        fileName = fileList[i];
        if (startsWith(fileName, searchString)){
            print(fileName);
            found_filename = fileName;
        }
    }
    return found_filename;
}


// MACRO STARTS HERE

//Welcome message
		Dialog.create("MicrogliaMorphology");
		Dialog.addMessage("Welcome to MicrogliaMorphology's ColorByCluster feature!");
		Dialog.addMessage("The complimentary ColorByCluster features within MicrogliaMorphology and MicrogliaMorphologyR allow you to color your microglia cells by their morphological cluster IDs.");
		Dialog.addMessage("This allows for visual verification of hypothesized cluster characterizations.");
		Dialog.addMessage("Please make sure to use the complimentary MicrogliaMorphologyR package to generate your ColorByCluster.csv file prior to this step.");
		Dialog.addMessage("If you have not done this yet, please do so first and come back to MicrogliaMorphology's ColorByCluster feature. If you are ready, continue on:");
		Dialog.show();

		Dialog.create("MicrogliaMorphology");
		Dialog.addMessage("If you want to batch process your images for ColorByCluster, please make sure that your original input images, thresholded images,"); 
		Dialog.addMessage("and ColorByCluster csv files all have the same starting string (see Github page for examples).");
		Dialog.show();
		

//use file browser to choose path and files to run plugin on

skipped_files = newArray();

// if batch mode = yes
	
	setOption("JFileChooser",true);
	
	// original input .tiff images
	ColorByCluster_originalimages_dir = getDirectory("Choose parent folder containing original .tiff images that you used as input for MicrogliaMorphology");
	ColorByCluster_originalimages = getFileList(ColorByCluster_originalimages_dir);
	ColorByCluster_originalimages = Array.sort(ColorByCluster_originalimages);
	ColorByCluster_originalimages_count = ColorByCluster_originalimages.length;
	
	// thresholded .tiff images
	ColorByCluster_thresholdedimages_dir = getDirectory("Choose parent folder containing thresholded .tiff images that were generated by MicrogliaMorphology from your original input images.");
	ColorByCluster_thresholdedimages = getFileList(ColorByCluster_thresholdedimages_dir);
	ColorByCluster_thresholdedimages = Array.sort(ColorByCluster_thresholdedimages);
	ColorByCluster_thresholdedimages_count = ColorByCluster_thresholdedimages.length;
	
	// ColorByCluster .csv files
	ColorByCluster_clusters_dir = getDirectory("Select the .csv files generated from MicrogliaMorphologyR which contain the cluster labels for the final microglia cells from your images.");
	ColorByCluster_clusters = getFileList(ColorByCluster_clusters_dir);
	ColorByCluster_clusters = Array.sort(ColorByCluster_clusters);
	ColorByCluster_clusters_count = ColorByCluster_clusters.length;
	
	// Directory to save final ColorByCluster images to 
	ColorByCluster_output = getDirectory("Select the directory you want to save your final ColorByCluster images to");
	
	// customize colors for up to 10 cluster labels
	Dialog.create("MicrogliaMorphology");
	Dialog.addMessage("In the sections below, specify what colors you want your morphological clusters to be in the image.");
	Dialog.addMessage("You can format your color choices as HEX code (e.g., BBCC33)");
	Dialog.addMessage("or any of the following colors: black, white, cyan, magenta, yellow, red, green, blue, and orange");
	Dialog.addString("Cluster 1:", "BBCC33");
	Dialog.addString("Cluster 2:", "44BB99");
	Dialog.addString("Cluster 3:", "EEDD88");
	Dialog.addString("Cluster 4:", "EE8866");
	Dialog.addString("Cluster 5:", "red");
	Dialog.addString("Cluster 6:", "green");
	Dialog.addString("Cluster 7:", "blue");
	Dialog.addString("Cluster 8:", "yellow");
	Dialog.addString("Cluster 9:", "orange");
	Dialog.addString("Cluster 10:", "cyan");
	Dialog.show();	
	
	Cluster1 = Dialog.getString();
	Cluster2 = Dialog.getString();
	Cluster3 = Dialog.getString();
	Cluster4 = Dialog.getString();
	Cluster5 = Dialog.getString();
	Cluster6 = Dialog.getString();
	Cluster7 = Dialog.getString();
	Cluster8 = Dialog.getString();
	Cluster9 = Dialog.getString();
	Cluster10 = Dialog.getString();
	clusters = newArray(Cluster1, Cluster2, Cluster3, Cluster4, Cluster5, Cluster6, Cluster7, Cluster8, Cluster9, Cluster10);
	
	// loop through original images
	for(i=0; i<ColorByCluster_originalimages_count; i++){
		// extract out common string to search for across folders from original .tiff files
		ColorByCluster_originalimage = ColorByCluster_originalimages[i];
		print(ColorByCluster_originalimage);
		StringToSearchFor = File.getNameWithoutExtension(ColorByCluster_originalimages_dir + ColorByCluster_originalimage); 
		// loop through ColorByCluster csv files folder, find the file with matching string, open, and print file name
		cluster_file = listFiles(ColorByCluster_clusters_dir, ColorByCluster_clusters, StringToSearchFor);
		// loop through thresholded folder, find the file with matching string, open, and print file name
		threshold_file = listFiles(ColorByCluster_thresholdedimages_dir, ColorByCluster_thresholdedimages, StringToSearchFor);
		
		// only do analysis if both the image and the csv file exist
		//could expand to also check for the thresholded image (if extension is known from previous step - I am not sure if it is a good idea to generalize that yet
		if (File.exists(ColorByCluster_originalimages_dir + ColorByCluster_originalimage) && File.exists(ColorByCluster_clusters_dir + cluster_file) && File.exists(ColorByCluster_thresholdedimages_dir + threshold_file)) {
			openFile(ColorByCluster_thresholdedimages_dir + threshold_file);
			
			openFile(ColorByCluster_clusters_dir + cluster_file);
			selectWindow(cluster_file);
			if (Table.getString("Region", 1) == "full_brain") {
				remove_regions = true;
			}
			selectWindow(threshold_file);
			if (remove_regions) {
				Overlay.remove;
				run("Select None");
			}
			
			//if there is not yet an overlay, make one
			if (Overlay.size < 1) {
				Overlay.remove;
				has_selection = selectionType();
				//either of the current selection or the entire brain if there is no current selection
				if (has_selection == -1) {
					run("Select All");
					setSelectionName("full_brain");
					Overlay.addSelection;
					run("Select None");
				} else {
					Overlay.addSelection;
					run("Select None");
				}
			}
			//retrieve how many regions we have to go through
			region_number = Overlay.size;
			for (current_region = 0; current_region < region_number; current_region++) {
				
				
				selectWindow(threshold_file);
				run("To ROI Manager");
				//Overlay.remove;//do not have to do this if the overlay just stays like this the entire time
			
				roiManager("select", current_region);
				region = Roi.getName;
				run("Duplicate...", "title=region");
				
				selectWindow(threshold_file);
				run("From ROI Manager");
				
				selectWindow("region");
		   		run("Select None");
				run("Analyze Particles...", "pixel add");
				//close(threshold_file);
				
				// open original .tiff file and print file name
				open(ColorByCluster_originalimages_dir + ColorByCluster_originalimage);
				
				
				
				roiManager("Show All without labels");
				roiManager("Set Color", "black");
				selectWindow(cluster_file);
				nrow = Table.size();
				
				
				// ColorByCluster
				for(n=0; n<nrow; n++) {
					selectWindow(cluster_file);
					region_in_table = Table.getString("Region", n);
					if (region_in_table == region) {
						cluster = Table.get("Cluster",n);
						if (cluster > 0 && cluster < 11) {
							label2 = Table.getString("ID",n);
							roi_idx = findRoiWithName(label2);
							selectWindow(ColorByCluster_originalimage);
							roiManager("Select", roi_idx);
							
							Roi.setFillColor(clusters[cluster-1]);	
						}	
					}
				}
				
				run("Flatten");
				
				// save into ColorByCluster images
				saveAs("Tiff", ColorByCluster_output + ColorByCluster_originalimage + "_ColorByCluster_" + region);
				// close everything
				close(ColorByCluster_originalimage + "_ColorByCluster_" + region + ".tif");
				close(cluster_file);
				close(ColorByCluster_originalimage);
				close("ROI Manager");
			}
		} else {
			skipped_files = Array.concat(skipped_files , ColorByCluster_originalimages[i]);
		}
	}
	print("done!");
	if (skipped_files.length > 0) {
		print("The following file(s) do(es) not have associated thresholds or csv files and was/were skipped: ");
		Array.print(skipped_files);
	}


