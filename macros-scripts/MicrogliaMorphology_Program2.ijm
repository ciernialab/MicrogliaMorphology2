//// Microglia Morphology ImageJ macro
//// Created by Jenn Kim on September 18, 2022
//// Updated January 21, 2026

// FUNCTIONS

// Auto thresholding 
function thresholding(input, output, filename) {
		print(input + filename);
		open(input + filename);
	
		// MEASURE AREA
		run("Set Measurements...", "area display redirect=None decimal=9");
		run("Measure");
		
		// THRESHOLD IMAGE AND CLEAN UP FOR DOWNSTREAM PROCESSING IN ANALYZESKELETON
		run("8-bit");
		// convert to grayscale to best visualize all positive staining
		run("Grays");
		// adjust the brighness and contrast to make sure you can visualize all microglia processes
		// in ImageJ, B&C are changed by updating the image's lookup table, so pixel values are unchanged

		//run("Brightness/Contrast...");
		run("Enhance Contrast", "saturated=0.35");
		// run Unsharp Mask filter to further increase contrast of image using default settings
		// this mask does not create details, but rather clarifies existing detail in image
		run("Unsharp Mask...", "radius=3 mask=0.60");
		// use despeckle function to remove salt&pepper noise generated by unsharp mask filter
		run("Despeckle");
		run("Auto Threshold", "method=&auto_method ignore_black white");
		if (roichoice){
			// exclude anything not within roi
			setBackgroundColor(0, 0, 0); 
			run("Clear Outside"); 
		}
		// use despeckle function to remove remaining single-pixel noise generated by thresholding
		run("Despeckle");
		// apply close function to connect any disconnected cell processes back to the rest of the cell
		// this function connects two dark pixels if they are separated by up to 2 pixels
		run("Close-");
		// after closing up cells, remove any outliers
		// replaces a bright or dark outlier pixel by the median pixels in the surrounding area if it deviates by more than the threshold value specified
		// here, bright outliers are targeted with pixel radius 2 and threshold of 50
		run("Remove Outliers...", "radius=2 threshold=50 which=Bright");
		// save thresholded + cleaned image -- this is the input for skeleton analysis below
		saveAs("Tiff", output + filename + "_thresholded");
		
		close(filename);

		close(filename + "_thresholded.tif");
	}
	
// Auto local thresholding 
function local_thresholding(input, output, filename) {
		print(input + filename);
		open(input + filename);
		
		// MEASURE AREA
		run("Set Measurements...", "area display redirect=None decimal=9");
		run("Measure");
	
		// THRESHOLD IMAGE AND CLEAN UP FOR DOWNSTREAM PROCESSING IN ANALYZESKELETON
		run("8-bit");
		// convert to grayscale to best visualize all positive staining
		run("Grays");
		// adjust the brighness and contrast to make sure you can visualize all microglia processes
		// in ImageJ, B&C are changed by updating the image's lookup table, so pixel values are unchanged

		//run("Brightness/Contrast...");
		run("Enhance Contrast", "saturated=0.35");
		// run Unsharp Mask filter to further increase contrast of image using default settings
		// this mask does not create details, but rather clarifies existing detail in image
		run("Unsharp Mask...", "radius=3 mask=0.60");
		// use despeckle function to remove salt&pepper noise generated by unsharp mask filter
		run("Despeckle");		
		run("Auto Local Threshold", "method=&autolocal_method radius=&autolocal_radius parameter_1=0 parameter_2=0 white");
		if (roichoice){
			// exclude anything not within roi
			setBackgroundColor(0, 0, 0); 
			run("Clear Outside"); 
		}
		// use despeckle function to remove remaining single-pixel noise generated by thresholding
		run("Despeckle");
		// apply close function to connect any disconnected cell processes back to the rest of the cell
		// this function connects two dark pixels if they are separated by up to 2 pixels
		run("Close-");
		// after closing up cells, remove any outliers
		// replaces a bright or dark outlier pixel by the median pixels in the surrounding area if it deviates by more than the threshold value specified
		// here, bright outliers are targeted with pixel radius 2 and threshold of 50
		run("Remove Outliers...", "radius=2 threshold=50 which=Bright");
		// save thresholded + cleaned image -- this is the input for skeleton analysis below
		saveAs("Tiff", output + filename + "_thresholded");
		
		close(filename);

		close(filename + "_thresholded.tif");

	}

//Generating Single Cell ROIs from thresholded images
//Generating Single Cell ROIs from thresholded images
function cellROI(input, output, filename, min, max){
		close("Results");
		print(input + filename);
    	open(input + filename);
    	
    	mainTitle=getTitle();
		dirCropOutput=output;
		
	    Overlay.remove;
		run("Set Measurements...", "area display redirect=None decimal=3");
		
		run("Analyze Particles...", "pixel add");
		roiManager("Measure");	
		roiManager("Show All");
		
		if (nResults > 0) {
			selectWindow("Results");
			area = Table.getColumn("Area");
			label = Table.getColumn("Label");
			close("Results");
			run("Set Measurements...", "area mean standard modal min centroid center perimeter bounding fit shape feret's integrated median skewness kurtosis area_fraction stack display redirect=None decimal=9");
			
			Table.create("All_results");
			for (i = 0; i < area.length; i++) {
		
				if((min < area[i]) && (area[i] < max)){
					label_temp = label[i];
					label_temp = label_temp.replace(':','_');
					roiManager("Select", i);
					run("Duplicate...", "title=" + label_temp);
					setBackgroundColor(0, 0, 0);
					run("Clear Outside");
	    			Overlay.remove;
	    			
	    			analyze(label_temp, filename);
	    			
					print(label_temp);
					print(i + "/" + area.length);
					close(label_temp);
				}
			}
			close(filename);
			roiManager("reset");
			selectWindow("All_results");
			saveAs("results", output + mainTitle + ".csv");
			close(mainTitle + ".csv");
			close("All_results");
			
			return " ";
		} else {
			close(filename);
			roiManager("reset");
			print("A problem occured in image " +  filename + ".");
			return(filename);
		}
    }


// Skeletonize/AnalyzeSkeleton
function analyze(name, image) {
	setThreshold(1, 255);
	
	run("To Selection");
	run("Measure");
	
	selectWindow("Results");
	cell_headings = String.getResultsHeadings();
	cell_headings = split(cell_headings, "\t");
	cell_headings = Array.deleteValue(cell_headings, " ");
	
	cell_results = newArray(cell_headings.length);
	selectWindow("Results");
	for (i = 0; i < cell_headings.length; i++) {
		cell_results[i] = Table.getString(cell_headings[i], 0);
	}
	
	close("Results");
	selectWindow("All_results");//store cell results in second table
	
	result_row_number = Table.size;
	
	Table.set("Image", result_row_number, image);
	
	
	for (i = 0; i < cell_headings.length; i++) {
		Table.set(cell_headings[i], result_row_number, cell_results[i]);
	}
	
	run("Convex Hull");
	run("Measure"); // this gets results for the hull data one for loop lower
	
	selectWindow("Results");
	hull_headings = String.getResultsHeadings();
	hull_headings = split(hull_headings, "\t");
	hull_headings = Array.deleteValue(hull_headings, " ");
	
	
	major_feret = getValue("Feret");
	minor_feret = getValue("MinFeret");
	span = major_feret / minor_feret;
	long_radii = get_long_radii();
	max_radius = get_max(long_radii);
	short_radii = get_short_radii(long_radii);
	min_radius = get_min(short_radii);
	radius_ratio = max_radius / min_radius;
	Array.getStatistics(Array.concat(long_radii, short_radii), min, max, mean, stdDev);
	coefficient_of_variation = stdDev / mean;
	
	hull_results = newArray(hull_headings.length);
	selectWindow("Results");
	for (i = 0; i < hull_headings.length; i++) {
		hull_results[i] = Table.getString(hull_headings[i], 0);
	}
	
	close("Results");
	selectWindow("All_results");
	for (i = 0; i < hull_headings.length; i++) {
		Table.set("hull_" + hull_headings[i], result_row_number, hull_results[i]);
	}
	
	Table.set("Hull span ratio", result_row_number, span);
	Table.set("Max/min radii from hull", result_row_number, radius_ratio);
	Table.set("Relative variation (CV) hull", result_row_number, coefficient_of_variation);
	
	//now do skeleton stuff
	run("Select None");
	run("Skeletonize (2D/3D)");
	//now that ROIs are obtained, can perform skeleton
	run("Analyze Skeleton (2D/3D)", "prune=none show");
	close("Branch information"); //do not need this (yet), could integrate & 
	close("Tagged skeleton");
	close(name + "-labeled-skeletons");
	
	//get column titles as array
	selectWindow("Results");
	skeleton_headings = String.getResultsHeadings();
	skeleton_headings = split(skeleton_headings, "\t");
	skeleton_headings = Array.deleteValue(skeleton_headings, " ");
	
	//get data associated with these headings
	skeleton_results = newArray(skeleton_headings.length);
	selectWindow("Results");
	for (i = 0; i < skeleton_headings.length; i++) {
		skeleton_results[i] = Table.getString(skeleton_headings[i], 0);
	}
	
	close("Results");
	selectWindow("All_results");
	for (i = 0; i < skeleton_headings.length; i++) {
		Table.set("skeleton_" + skeleton_headings[i], result_row_number, skeleton_results[i]);
	}
	
}
function get_long_radii() {
	center_of_mass_x = getValue("XM");
	center_of_mass_y = getValue("YM");
	
	getSelectionCoordinates(xpoints, ypoints);
	
	//from each node of the ROI get the distance to the center of mass
	all_corner_distances = newArray(xpoints.length);
	for (i = 0; i < all_corner_distances.length; i++) {
		all_corner_distances[i] = sqrt(abs(Math.pow(xpoints[i] - center_of_mass_x, 2) + Math.pow(ypoints[i] - center_of_mass_y, 2)));
	}
	
	return all_corner_distances;
}

function get_short_radii(long_radii) {
	getSelectionCoordinates(xpoints, ypoints);
	//from each edge of the ROI get the smallest distance to the center of mass (height of triangle when edge is the base)
	all_midpoint_distances = newArray(long_radii.length);
	
	//Heron's formula to calc height of triangle: get length of all sides
	//gotta start with the connection between the first and last point outside the loop
	base_length = sqrt(abs(Math.pow(xpoints[0] - xpoints[xpoints.length - 1], 2) + Math.pow(ypoints[0] - ypoints[ypoints.length - 1], 2)));
	//the two distances of the corners to the center of mass were already calculated
	side_a_length = long_radii[0];
	side_b_length = long_radii[long_radii.length - 1];
	
	semiperimeter = (side_a_length + base_length + side_b_length) / 2;
	triangle_area = sqrt(semiperimeter * (semiperimeter - side_a_length) * (semiperimeter - base_length) * (semiperimeter - side_b_length));
	all_midpoint_distances[0] = 2 * triangle_area / base_length;
	
	//now do the same for the rest of the point pairs 
	for (i = 1; i < all_midpoint_distances.length; i++) {
		base_length = sqrt(abs(Math.pow(xpoints[i] - xpoints[i - 1], 2) + Math.pow(ypoints[i] - ypoints[i - 1], 2)));
		side_a_length = long_radii[i];
		side_b_length = long_radii[i - 1];
		
		semiperimeter = (side_a_length + base_length + side_b_length) / 2;
		triangle_area = sqrt(semiperimeter * (semiperimeter - side_a_length) * (semiperimeter - base_length) * (semiperimeter - side_b_length));
		all_midpoint_distances[i] = 2 * triangle_area / base_length;
	}
	
	return all_midpoint_distances;
}

//get the maximum value of an array
function get_max(array) {
	candidate = array[0];
	for (i = 1; i < array.length; i++) {
		candidate = maxOf(candidate, array[i]);
	}
	return candidate;
}

//get the maximum value of an array
function get_min(array) {
	candidate = array[0];
	for (i = 1; i < array.length; i++) {
		candidate = minOf(candidate, array[i]);
	}
	return candidate;
}

// choices in drop-down prompts for MicrogliaMorphology macro
thresholding_approach = newArray("Auto thresholding", "Auto local thresholding");
thresholding_parameters = newArray("Huang","Huang2","Intermodes","IsoData","Li","MaxEntropy","Mean","MinError(I)","Minimum","Moments","Otsu","Percentile","RenyiEntropy","Shanbhag","Triangle","Yen")
thresholding_parameters2 = newArray("Bernsen","Contrast","Mean","Median","MidGrey","Niblack","Otsu","Phansalkar","Sauvola");



// MACRO STARTS HERE

//Welcome message
		Dialog.create("MicrogliaMorphology");
		Dialog.addMessage("Welcome to Microglia Morphology!");
		Dialog.addMessage("We will first specify some dataset-specific parameters before running MicrogliaMorphology.");
		Dialog.addMessage("Please make sure to use the BioVoxxel ImageJ plugin to determine your thresholding parameters prior to this step.");
		Dialog.addMessage("If you have not done this yet, please do so first and come back to MicrogliaMorphology. If you have, continue on :");
		Dialog.show();

// STEP 1a. Specifying final dataset-specific parameters: thresholding
			
		//dialog box
		Dialog.create("MicrogliaMorphology");
		Dialog.addChoice("Are you using auto thresholding or auto local thresholding?", thresholding_approach);
		Dialog.addMessage("If you are using auto thresholding:");
		Dialog.addChoice("Which method is best for your dataset?", thresholding_parameters);
		Dialog.addMessage("If you are using auto local thresholding:");
		Dialog.addChoice("Which method is best for your dataset?", thresholding_parameters2);
		Dialog.addNumber("Radius:", 100);
		Dialog.addCheckbox("Does your test image have ROIs traced?", true);
		Dialog.addMessage("Next, let's determine the area range of a single microglial cell using a test image.");
		
		Dialog.addCheckbox("Use test image to find Microglia areas?", true);
		Dialog.addCheckbox("Create output folders automatically?", false);
		Dialog.addCheckbox("Is your input already binarized (thresholded)?", false);
		
		Dialog.show();
		
		auto_or_autolocal = Dialog.getChoice();
		auto_method = Dialog.getChoice();
		autolocal_method= Dialog.getChoice();
		autolocal_radius = Dialog.getNumber();
		roichoicetest=Dialog.getCheckbox();
		use_test_image = Dialog.getCheckbox();
		use_directory_creation = Dialog.getCheckbox();
		skip_thresholding = Dialog.getCheckbox();
		
		
// STEP 1b. Determining single cell area range using test image
		if (use_test_image == true) {
			//use file browser to choose test image
			path = File.openDialog("Open your test image");
			open(path);
				
			// Apply all steps before you would get to single cell extractions	
				if(auto_or_autolocal == "Auto thresholding"){
					// THRESHOLD IMAGE AND CLEAN UP FOR DOWNSTREAM PROCESSING IN ANALYZESKELETON
					run("8-bit");
					// convert to grayscale to best visualize all positive staining
					run("Grays");
					// adjust the brighness and contrast to make sure you can visualize all microglia processes
					// in ImageJ, B&C are changed by updating the image's lookup table, so pixel values are unchanged
	
					//run("Brightness/Contrast...");
					run("Enhance Contrast", "saturated=0.35");
					// run Unsharp Mask filter to further increase contrast of image using default settings
					// this mask does not create details, but rather clarifies existing detail in image
					run("Unsharp Mask...", "radius=3 mask=0.60");
					// use despeckle function to remove salt&pepper noise generated by unsharp mask filter
					run("Despeckle");
					run("Auto Threshold", "method=&auto_method ignore_black white");
					if (roichoicetest){
						// exclude anything not within roi
						setBackgroundColor(0, 0, 0); 
						run("Clear Outside"); 
					}				
					// use despeckle function to remove remaining single-pixel noise generated by thresholding
					run("Despeckle");
					// apply close function to connect any disconnected cell processes back to the rest of the cell
					// this function connects two dark pixels if they are separated by up to 2 pixels
					run("Close-");
					// after closing up cells, remove any outliers
					// replaces a bright or dark outlier pixel by the median pixels in the surrounding area if it deviates by more than the threshold value specified
					// here, bright outliers are targeted with pixel radius 2 and threshold of 50
					run("Remove Outliers...", "radius=2 threshold=50 which=Bright");
					
					// analyze particles
					run("Set Measurements...", "area display redirect=None decimal=3");
					run("Analyze Particles...", "pixel add");
					roiManager("Show All");
				}
			
				if(auto_or_autolocal == "Auto local thresholding"){
					// THRESHOLD IMAGE AND CLEAN UP FOR DOWNSTREAM PROCESSING IN ANALYZESKELETON
					run("8-bit");
					// convert to grayscale to best visualize all positive staining
					run("Grays");
					// adjust the brighness and contrast to make sure you can visualize all microglia processes
					// in ImageJ, B&C are changed by updating the image's lookup table, so pixel values are unchanged
	
					//run("Brightness/Contrast...");
					run("Enhance Contrast", "saturated=0.35");
					// run Unsharp Mask filter to further increase contrast of image using default settings
					// this mask does not create details, but rather clarifies existing detail in image
					run("Unsharp Mask...", "radius=3 mask=0.60");
					// use despeckle function to remove salt&pepper noise generated by unsharp mask filter
					run("Despeckle");		
					run("Auto Local Threshold", "method=&autolocal_method radius=&autolocal_radius parameter_1=0 parameter_2=0 white");
					if (roichoicetest){
						// exclude anything not within roi
						setBackgroundColor(0, 0, 0); 
						run("Clear Outside"); 
					}					
					// use despeckle function to remove remaining single-pixel noise generated by thresholding
					//run("Despeckle");
					// apply close function to connect any disconnected cell processes back to the rest of the cell
					// this function connects two dark pixels if they are separated by up to 2 pixels
					run("Close-");
					// after closing up cells, remove any outliers
					// replaces a bright or dark outlier pixel by the median pixels in the surrounding area if it deviates by more than the threshold value specified
					// here, bright outliers are targeted with pixel radius 2 and threshold of 50
					run("Remove Outliers...", "radius=2 threshold=50 which=Bright");
					
					// analyze particles
					run("Set Measurements...", "area display redirect=None decimal=3");
					run("Analyze Particles...", "pixel add");
					roiManager("Show All");
				}		
					
			waitForUser("Select a particle that you would consider TOO SMALL to be a single microglia cell and click letter m on your keyboard to measure its area. Do this a total of 5 times. Don't click OK until you're done with this!");
			run("Summarize");
			area_min = getResult("Area");
			
			close("Results");
			waitForUser("Select a particle that you would consider TOO BIG to be a single microglia cell and click letter m on your keyboard to measure its area. Do this a total of 5 times. Don't click OK until you're done with this!");
			run("Summarize");
			//number_rows = nResults;
			area_max = getResult("Area", nResults-2);
			
			// close everything before starting with rest of macro
			close("Results");
			selectImage(nImages());
			run("Close");
			close("ROI Manager");
		    close("B&C");
		} else {
			Dialog.create("Area Specification");
			Dialog.addNumber("Custom min area:", 0, 2, 12, "px^2");
			Dialog.addNumber("Custom max area:", 0, 2, 12, "px^2");
			Dialog.show();
			area_min = Dialog.getNumber();
			area_max = Dialog.getNumber();
		}
	    
	    //everything to do with thresholding and microscopy input choice can be skipped
	    if (skip_thresholding == false) {
		    //get file input directory here to be able to compare with the test image directory
			subregion_dir = getDirectory("Choose folder containing original input images");
			parent_directory = File.getDirectory(subregion_dir);
			subregion_input = getFileList(subregion_dir);
			subregion_input = Array.sort(subregion_input);
			autocount=subregion_input.length;
			
			if (use_test_image == false) {
				path = subregion_dir;
			}
			
		    // conditional printing for saving final parameters
			if(auto_or_autolocal == "Auto thresholding"){
				finalprint = auto_method;
			}
			if(auto_or_autolocal == "Auto local thresholding"){
				finalprint = autolocal_method + ", radius = " + autolocal_radius;
			}
	
		    
					
	// Progress message: print summary statement of parameters
			Dialog.create("MicrogliaMorphology");
			Dialog.addMessage("Here is a summary of your dataset-specific parameters that will be applied in MicrogliaMorphology");
			Dialog.addMessage("AUTO THRESHOLDING OR AUTO LOCAL THRESHOLDING?");
			Dialog.addMessage(auto_or_autolocal);
			
			if(auto_or_autolocal == "Auto thresholding"){
				Dialog.addMessage("METHOD:");
				Dialog.addMessage(auto_method);
			}
			
			if(auto_or_autolocal == "Auto local thresholding"){
				Dialog.addMessage("METHOD:");
				Dialog.addMessage(autolocal_method);
				Dialog.addMessage("RADIUS:");
				Dialog.addMessage(autolocal_radius);
				}
			
			Dialog.addMessage("LOWER CELL AREA FILTER:");
			Dialog.addMessage(area_min);
			Dialog.addMessage("UPPER CELL AREA FILTER:");
			Dialog.addMessage(area_max);
			Dialog.addMessage("Now we will proceed with thresholding your images!");
			Dialog.show();
			
	// STEP 2. Thresholding
	
	//use file browser to choose path and files to run plugin on
			
			
			if (use_directory_creation) {
				//create directory tree
				thresholded_dir = parent_directory + "/ThresholdedImages/";
				data_output=parent_directory + "/MeasurementResults/";
				File.makeDirectory(thresholded_dir);
				File.makeDirectory(data_output);
			} else {
				thresholded_dir=getDirectory("Choose output folder to write thresholded images to");
				
				data_output=getDirectory("Choose output folder to write measurement results to");
				
			}
		
			//save parameter files in parent of output, in case of input being read-only
			output_parent=File.getParent(thresholded_dir);
			f = File.open(output_parent + "/FinalDatasetParameters.txt");
			if (use_test_image) {
				print(f, auto_or_autolocal + " \n" + 
			    	"Thresholding method = " + finalprint + " \n" +
			    	"Lower cell area filter = " + area_min + " \n" + 
			    	"Upper cell area filter = " + area_max + " \n" + 
			    	"Setup ran on image=" + path);
			} else {
				print(f, auto_or_autolocal + " \n" + 
		    		 "Thresholding method = " + finalprint + " \n" +
		    		 "Lower cell area filter = " + area_min + " \n" + 
		    		 "Upper cell area filter = " + area_max);
			}
		    File.close(f);
			
	
			//dialog box
			Dialog.create("MicrogliaMorphology");
			Dialog.addMessage("Processing files from directory:");
			parentname=split(subregion_dir,"/");
			Dialog.addMessage(parentname[(parentname.length)-1]);
			Dialog.addMessage("which has this many images:");
			Dialog.addMessage(autocount);
			Dialog.addMessage("Select range of images you'd like to analyze");
			Dialog.addNumber("Start at Image:", 1);
			Dialog.addNumber("Stop at Image:", 1);
			Dialog.addCheckbox("Do your input images have ROIs traced?", true);
			Dialog.show();
					
			startAt=Dialog.getNumber();
			endAt=Dialog.getNumber();
			roichoice=Dialog.getCheckbox();
			
			setBatchMode(true);
			
				
			if(auto_or_autolocal == "Auto thresholding"){
				
				for (i=(startAt-1); i<(endAt); i++){
				
					if (use_batchmode) {
						print("Thresholding in progress, image " + (i + 1) + " out of " + endAt); //have some kind of update while in batchmode
					} 
					thresholding(subregion_dir, thresholded_dir, subregion_input[i]);
					}
			}
			
			if(auto_or_autolocal == "Auto local thresholding"){
			
				for (i=(startAt-1); i<(endAt); i++){
				
					print("Thresholding in progress, image " + (i + 1) + " out of " + endAt); //have some kind of update while in batchmode
					
					local_thresholding(subregion_dir, thresholded_dir, subregion_input[i]);
				}
			}
			
			
			// SAVE AREA MEASURES
			saveAs("Results", output_parent + "/Areas.csv");
			close("Results");
			
			print("Thresholding finished");
			
			thresholded_input = getFileList(thresholded_dir);
			thresholded_input=Array.sort(thresholded_input);
			
			
			//use all thresholded images
			startAt = 1;
			endAt = thresholded_input.length;
			

	    } //this is all skipped, if thresholding is skipped
	    if (skip_thresholding == true) {
	    	thresholded_dir = getDirectory("Choose input folder of thresholded images");
	    	
			thresholded_input = getFileList(thresholded_dir);
			thresholded_input=Array.sort(thresholded_input);
	    	//dialog box
			Dialog.create("MicrogliaMorphology");
			Dialog.addMessage("Processing files from directory:");
			parentname=split(thresholded_dir,"/");
			Dialog.addMessage(parentname[(parentname.length)-1]);
			Dialog.addMessage("which has this many images:");
			Dialog.addMessage(thresholded_input.length);
			Dialog.addMessage("Select range of images you'd like to analyze");
			Dialog.addNumber("Start at Image:", 1);
			Dialog.addNumber("Stop at Image:", thresholded_input.length);
			Dialog.show();
					
			startAt=Dialog.getNumber();
			endAt=Dialog.getNumber();
			

	    	if (use_directory_creation) {
				
				data_output = File.getParent(thresholded_dir) + "/MeasurementResults/";
				File.makeDirectory(data_output);
			} else {
				
				data_output=getDirectory("Choose output folder to write measurement results to");
				
			}
			
			setBatchMode(true);

	    }

		
		skipped_files = newArray();
		for (i=(startAt-1); i<(endAt); i++){
			
			print("Measuring, image " + (i + 1) + " out of " + endAt); //have some kind of update while in batchmode
			
			skipping = cellROI(thresholded_dir, data_output, thresholded_input[i], area_min, area_max);
			skipped_files = Array.concat(skipped_files, skipping);
			run("Collect Garbage");
		}
		
		skipped_files = Array.deleteValue(skipped_files, " ");
		
		setBatchMode(false);
		
		
	    print("Finished analysing cells. The following files were skipped: " + String.join(skipped_files, " "));
		close("*");
		close("ROI Manager");
		
	    print("done!");
